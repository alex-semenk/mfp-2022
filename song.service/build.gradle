plugins {
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'com.mfp'
version = '1.0.0'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	runtimeOnly 'com.h2database:h2'
	// runtimeOnly 'org.postgresql:postgresql'

	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

testing {
	suites {
		test {
			useJUnitJupiter()
			dependencies {
				implementation 'org.springframework.boot:spring-boot-starter-test'
				implementation 'org.mockito:mockito-core:4.5.1'
				implementation 'net.bytebuddy:byte-buddy:1.12.9'
			}
		}

		testIntegration(JvmTestSuite) {
			dependencies {
				implementation project
				implementation 'org.springframework.boot:spring-boot-starter-test'
				implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
			}

			targets {
				all {
					testTask.configure {
						shouldRunAfter(test)
					}
				}
			}
		}

		testContract(JvmTestSuite) {
			dependencies {
				implementation project
				implementation 'org.springframework.boot:spring-boot-starter-test'
				implementation 'au.com.dius:pact-jvm-provider-junit5:4.0.10'
				implementation 'au.com.dius:pact-jvm-provider-junit5-spring:4.0.10'
				implementation 'org.springframework.boot:spring-boot'
				implementation 'org.springframework:spring-web:5.3.21'
			}
			targets {
				all {
					testTask.configure {
						shouldRunAfter(testIntegration)
					}
				}
			}
		}

		testComponent(JvmTestSuite) {
			dependencies {
				implementation project
				implementation 'org.junit.platform:junit-platform-suite'
				implementation "org.assertj:assertj-core:${assertjVersion}"
				implementation project.dependencies.platform("io.cucumber:cucumber-bom:${cucumberVersion}")
				implementation 'io.cucumber:cucumber-java'
				implementation 'io.cucumber:cucumber-spring'
				implementation 'io.cucumber:cucumber-junit-platform-engine'
				implementation 'org.springframework.boot:spring-boot-starter'
				implementation 'org.springframework.boot:spring-boot-starter-test'
				implementation 'org.springframework.boot:spring-boot-starter-web'
				implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
				implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

				compileOnly "org.projectlombok:lombok:${lombokVersion}"
				// annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
			}
			dependencyManagement {
				imports {
					mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2021.0.1'
				}
			}
			targets {
				all {
					testTask.configure {
						/*
						 * System property will include the feature name in the cucumber scenario name.
						 * This makes the test results legible.
						 */
						systemProperty("cucumber.junit-platform.naming-strategy", "long")
						shouldRunAfter(testContract)
					}
				}
			}
		}
	}
}

dependencies {
	testComponentAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}" // !!!!!
}

tasks.named('check') {
	dependsOn(testing.suites.testIntegration)
	dependsOn(testing.suites.testContract)
	dependsOn(testing.suites.testComponent)
}
